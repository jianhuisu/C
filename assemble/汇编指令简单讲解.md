# 汇编指令简单讲解

先看一个简单的demo.

	#PURPOSE: This program finds the maximum number of a
	#	  set of data items.
	#
	#VARIABLES: The registers have the following uses:
	#
	# %edi - Holds the index of the data item being examined
	# %ebx - Largest data item found
	# %eax - Current data item
	#
	# The following memory locations are used:
	#
	# data_items - contains the item data. A 0 is used
	# to terminate the data
	#

	 .section .data
	data_items: 		#These are the data items
	 .long 3,67,34,222,45,75,54,34,44,33,22,11,66,0

	 .section .text
	 .globl _start
	_start:
	 movl $0, %edi  	# move 0 into the index register
	 movl data_items(,%edi,4), %eax # load the first byte of data
	 movl %eax, %ebx 	# since this is the first item, %eax is
				# the biggest

	start_loop: 		# start loop
	 cmpl $0, %eax  	# check to see if we've hit the end
	 je loop_exit
	 incl %edi 		# load next value
	 movl data_items(,%edi,4), %eax
	 cmpl %ebx, %eax 	# compare values
	 jle start_loop 	# jump to loop beginning if the new
	 			# one isn't bigger
	 movl %eax, %ebx 	# move the value as the largest
	 jmp start_loop 	# jump to loop beginning

	loop_exit:
	 # %ebx is the status code for the _exit system call
	 # and it already has the maximum number
	 movl $1, %eax  	#1 is the _exit() syscall
	 int $0x80


汇编程序中以`.`开头的名称并不是指令的助记符，不会被翻译成机器指令，而是给汇编器一些特殊指示，称为汇编指示（Assembler Directive）或伪操作（Pseudo-operation），由于它不是真正的指令所以加个“伪”字。

`.section`指示把代码划分成若干个段`Section`，程序被操作系统加载执行时，每个段被加载到不同的地址，操作系统对不同的页面设置不同的读、写、执行权限。

对照上文汇编代码`.section`指令

 - `.data`段保存程序的数据，是可读可写的，相当于C程序的全局变量。本程序中的`.long`处.
 - `.text`段保存代码，是只读和可执行的，后面那些指令都属于`.text`段.

C语言的全局变量如果在代码中没有初始化，就会在程序加载时用`0`初始化。这种数据属于`.bss`段，在加载时它和`.data`段一样都是可读可写的数据，但是在ELF文件中`.data`段需要占用一部分空间保存初始值，而`.bss`段则不需要。也就是说，`.bss`段在文件中只占一个`Section Header`而没有对应的`Section`.

继续看.text段中的汇编指令

`_start`是一个符号（Symbol），符号在汇编程序中代表一个地址，可以用在指令中，汇编程序经过汇编器的处理之后，所有的符号都被替换成它所代表的地址值。在C语言中我们通过变量名访问一个变量，其实就是读写某个地址的内存单元，我们通过函数名调用一个函数，其实就是跳转到该函数第一条指令所在的地址，所以变量名和函数名都是符号，本质上是代表内存地址的。	



